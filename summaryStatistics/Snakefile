import os

DATA = {
    'annotationFiles' : '../data/annotationFiles'
}

DIRECTORIES = {
    'data' : "data",
    'intermediateFiles' : "intermediateFiles",
    'figures' : "figures"  
}    

SCRIPTS = {
    'plot_summaryStats' : "scripts/plot.R",
    'plot_heatmap' : "scripts/heatmap.R"
}

REGIONS = ['broadDomains','hotRegions','stretchEnhancer','superEnhancers','typicalEnhancers']
CELLS = ['GM12878','H1','HepG2','K562']

rule final:
    """ Plot summary statistics for annotations """
    input:
        os.path.join(DIRECTORIES['figures'], "fig.summaryStatistics.pdf"),
        os.path.join(DIRECTORIES['figures'], "fig.overlapHeatmap.pdf")


rule getNumberOfSegments:
    """ Get number of segments for each annotation of each cell type """
    input:
        expand(os.path.join(DATA['annotationFiles'], '{cell}.{region}.annotations.bed'), cell = CELLS, region = REGIONS)
    output:
        main = os.path.join(DIRECTORIES['intermediateFiles'], "numberOfSegments.dat"),
        tempfile = temp(os.path.join(DIRECTORIES['intermediateFiles'], "numberOfSegments.dat.temp"))
    shell:
        r"""
        for i in {input}; do b=`basename $i .annotations.bed | sed -e 's:\.:\t:g'`; less $i | sortBed -i - | mergeBed -i - | wc -l | awk '{{print "'"$b"'""\t"$NF}}' OFS='\t' ; done > {output.tempfile} ;
        echo -e "cell\tannotation\tnumber" | cat - {output.tempfile} > {output.main}
        """

rule getLengthDistributionOfSegments:
    """ Get length of segments for each annotation of each cell type """
    input:
        expand(os.path.join(DATA['annotationFiles'], '{cell}.{region}.annotations.bed'), cell = CELLS, region = REGIONS)
    output:
        main = os.path.join(DIRECTORIES['intermediateFiles'], "lengthDistributionOfSegments.dat"),
        tempfile = temp(os.path.join(DIRECTORIES['intermediateFiles'], "lengthDistributionOfSegments.dat.temp")),
    shell:
        r"""
        for i in {input}; do b=`basename $i .annotations.bed | sed -e 's:\.:\t:g'`; less $i | awk '{{print $3-$2}}' | awk '{{print "'"$b"'""\t"$0}}' OFS='\t'; done > {output.tempfile} ;
        echo -e "cell\tannotation\tlength" | cat - {output.tempfile} > {output.main}
        """

rule getGenomeCoverage:
    """ Get % genome coverage of segments for each annotation of each cell type """
    input:
        expand(os.path.join(DATA['annotationFiles'], '{cell}.{region}.annotations.bed'), cell = CELLS, region = REGIONS)
    output:
        main = os.path.join(DIRECTORIES['intermediateFiles'], "genomeCoverage.dat"),
        tempfile = temp(os.path.join(DIRECTORIES['intermediateFiles'], "genomeCoverage.dat.temp"))
    params:
        genomeSize = 3095677412 
    shell:
        r"""
        for i in {input}; do b=`basename $i .annotations.bed | sed -e 's:\.:\t:g'`; less $i | awk '{{sum+=$3-$2}} END {{print "{params.genomeSize}\t""'"$b"'""\t"sum}}' OFS='\t'; done > {output.tempfile} ;
        echo -e "length_genome\tcell\tannotation\tlength_annotation" | cat - {output.tempfile} > {output.main}
        """
        
rule plot_summaryStats:
    """ Plot summary statistics"""
    input:
        numberOfSegments = rules.getNumberOfSegments.output.main,
        lengthDistributionOfSegments = rules.getLengthDistributionOfSegments.output.main,
        genomeCoverage = rules.getGenomeCoverage.output.main
    output:
        os.path.join(DIRECTORIES['figures'], "fig.summaryStatistics.pdf")
    params:
        script = SCRIPTS['plot_summaryStats']
    shell:
        r"""
        Rscript {params.script} {input.numberOfSegments} {input.lengthDistributionOfSegments} {input.genomeCoverage} {output} 
        """

rule getOverlapFractions:
    """Get overlap fraction between annotations """
    input:
        expand(os.path.join(DATA['annotationFiles'], '{cell}.{region}.annotations.bed'), cell = CELLS, region = REGIONS)
    output:
        main = os.path.join(DIRECTORIES['intermediateFiles'], "overlapFraction.dat"),
        tempfile = temp(os.path.join(DIRECTORIES['intermediateFiles'], "overlapFraction.dat.temp")) 
    shell:
        r"""
        for i in {input}; do f1=`basename $i .annotations.bed | sed -e 's:\.:\t:g'`; len_f1=`less $i | awk 'BEGIN{{SUM=0}}{{ SUM+=$3-$2 }}END{{print SUM}}'`; for j in `ls {input} | grep -v "$i"`; do f2=`basename $j .annotations.bed | sed -e 's:\.:\t:g'`; len_f2=`less $j | awk 'BEGIN{{SUM=0}}{{ SUM+=$3-$2 }}END{{print SUM}}'`; overlap=`intersectBed -a $i -b $j | awk 'BEGIN{{SUM=0}}{{ SUM+=$3-$2 }}END{{print SUM}}'` ; echo -e "$f1\t${{len_f1}}\t$f2\t${{len_f2}}\t$overlap" ; done; done > {output.tempfile} ;
        echo -e "cell1\tfeature1\tlenght1\tcell2\tfeature2\tlength2\toverlap" | cat - {output.tempfile} > {output.main}
        """

rule plot_overlapFractions:
    """Plot overlap fraction heatmap """
    input:
        rules.getOverlapFractions.output.main
    output:
        os.path.join(DIRECTORIES['figures'], "fig.overlapHeatmap.pdf")
    script:
        SCRIPTS['plot_heatmap']
