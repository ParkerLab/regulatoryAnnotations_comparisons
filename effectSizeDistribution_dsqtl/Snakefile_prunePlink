import pandas

"""
Workflow for pruning a list od SNPs using P value of association, using 1000g phase 3 vcf. Followed by fetching proxy SNPs, also using 1000g. Population codes or Superpopulation codes can be used to subset 1000g samples  
Include this workflow as part of other workflow requiring proxies 
Vcf files first subset by the selected population. All indels are removed. Also, plink files only contain bialleic SNPs. .
Pruning is done using --clump flags
"""

rule makeSampleFile:
    input:
        sampleInfo = DATA['vcf_sampleInfo']
    output:
        samplefile = os.path.join(config['output_directory'], "subsetSamples.txt")
    run:
        d = pandas.read_csv(input.sampleInfo, sep='\t')
        d = d[d[config['population_type']] == config['population_code']]
        d[['Sample name']].to_csv(output[0], header=False, index=False)

rule subsetVCF:
    input:
        snpfile = DATA['1000g'],
        samplefile = rules.makeSampleFile.output.samplefile,
        posfile = os.path.join(DIRECTORIES['intermediateFiles'], "dsqtl.RsID.txt")
    output:
        vcf = temp(os.path.join(config['output_directory'], "chr{chrom}.maf{maf}.recode.vcf.gz")),
        index = temp(os.path.join(config['output_directory'], "chr{chrom}.maf{maf}.recode.vcf.gz.tbi")),
    params:
        outstring = os.path.join(config['output_directory'], "chr{chrom}.maf{maf}"),
    shell:
        r"""
        vcftools --gzvcf {input.snpfile}  --keep {input.samplefile} \
        --remove-indels \
        --snps {input.posfile} \
        --maf {wildcards.maf} \
        --out {params.outstring} --recode ;
        bgzip {params.outstring}.recode.vcf
        tabix {output.vcf}
        """

rule get_plink_files:
    """
    Make plink format input files after filtering 1000g vcf. IMP - vcf files are usually large so designated to be temp
    plink map and ped files only contain bi-allelic loci. 
    """
    input:
        vcf = expand(os.path.join(config['output_directory'], "chr{chrom}.maf{{maf}}.recode.vcf.gz"), chrom = CHROM),
        index = expand(os.path.join(config['output_directory'], "chr{chrom}.maf{{maf}}.recode.vcf.gz.tbi"), chrom = CHROM)
    output:
        vcf = os.path.join(config['output_directory'], "myfile.maf{maf}.recode.vcf.gz"),
        mapfile = temp(os.path.join(config['output_directory'], "myfile.maf{maf}.map")),
        pedfile = temp(os.path.join(config['output_directory'], "myfile.maf{maf}.ped")),
    params:
        outstring = os.path.join(config['output_directory'], "myfile.maf{maf}"),
    shell:
        r"""
        vcf-concat {input.vcf} | bgzip -c > {output.vcf} ;
        vcftools --gzvcf {output.vcf} --plink --out {params.outstring}
        """

rule prune_plink:
    input:
        mapfile = rules.get_plink_files.output.mapfile,
        pedfile = rules.get_plink_files.output.pedfile,
        inputfile = os.path.join(DIRECTORIES['intermediateFiles'], "dsqtl.withRsID.dat") 
    output:
        clumpedfile = os.path.join(config['output_directory'], "myfile.maf{maf}.prune{prune_r2}.clumped"),
    params:
        outstring = os.path.join(config['output_directory'], "myfile.maf{maf}.prune{prune_r2}"),#rules.get_plink_files.params.outstring,
        instring = os.path.join(config['output_directory'], "myfile.maf{maf}"),
        p1 = config['plink_p1'],
        p2 = config['plink_p2'],
    shell:
        r"""
        /lab/sw/modules/plink/1.9/bin/plink --file {params.instring} \
        --clump {input.inputfile} --clump-r2 {wildcards.prune_r2} --clump-p1 {params.p1} --clump-p2 {params.p2} \
        --out {params.outstring}
        """
        
rule organize_pruned_results:
    input:
        pruned = rules.prune_plink.output.clumpedfile,
        inputfile = rules.prune_plink.input.inputfile
    output:
        main = os.path.join(DIRECTORIES['intermediateFiles'], "pruned.maf{maf}.prune{prune_r2}.dat"),
        input_for_proxy = temp(os.path.join(DIRECTORIES['intermediateFiles'], "pruned_input_for_proxy.maf{maf}.prune{prune_r2}.dat")),
    params:
        retain = "onlyPlinkOutput" # Only retain plink output clump lead SNPs, don't include triallelic SNPs that didn't get into plink or were missing from 1000g, as they could cause redundancy in later steps. Other option is "alsoMissing" to take all SNPs after subtracting the clumped SNPs, which would retain any missing SNPs from plink input.
    run:
        d = pandas.read_csv(input.pruned, delim_whitespace=True)
        deqtl = pandas.read_csv(input.inputfile, sep='\t')
        deqtl.rename(columns={'end':'pos'}, inplace=True)
        deqtl.loc[:,'chrom'] = deqtl['chrom'].astype(str)
        if params.retain == "alsoMissing":
            snps_to_remove = pandas.DataFrame(d[d['SP2'] != "NONE"]['SP2'].str.replace('\(1\)', '').str.split(',').tolist()).stack().reset_index().loc[:,0].tolist() 
            dout = deqtl[~ deqtl['SNP'].isin(snps_to_remove)]
        elif params.retain == "onlyPlinkOutput":
            d.rename(columns={'CHR':'chrom','BP':'pos'}, inplace=True)
            d.loc[:,'chrom'] = d['chrom'].astype(str)
            dout = pandas.merge(deqtl, d, how="inner", on=['chrom','pos'])
        dout.to_csv(output.main, sep='\t', index=False)
        dout.loc[:,'chrom'] = dout['chrom'].str.replace('chr','')
        dout[['chrom','pos']].to_csv(output.input_for_proxy, header=False, index=False, sep='\t')
        

rule setup_vcfTools:
    input:
        snpfile = DATA['1000g'],
        samplefile = rules.makeSampleFile.output.samplefile,
        posfile = rules.organize_pruned_results.output.input_for_proxy,
    output:
        os.path.join(config['output_directory'], "pruned{prune_r2}.maf{maf}.ld{proxy_r2}.chr{chrom}.list.geno.ld")
    params:
        outstring = os.path.join(config['output_directory'], "pruned{prune_r2}.maf{maf}.ld{proxy_r2}.chr{chrom}"),
        ldwindow = config['ldwindow'],
    shell:
        r"""            
        vcftools --gzvcf {input.snpfile}  --keep {input.samplefile}  --remove-indels  --geno-r2-positions {input.posfile} \
        --ld-window-bp {params.ldwindow} --min-r2 {wildcards.proxy_r2}  --out {params.outstring}
        """
        
rule mergeWithOriginal:
    input:
        proxies = expand(os.path.join(config['output_directory'], "pruned{{prune_r2}}.maf{{maf}}.ld{{proxy_r2}}.chr{chrom}.list.geno.ld"), chrom=CHROM),
        main = rules.organize_pruned_results.output.main,
    output:
        main = os.path.join(DIRECTORIES['intermediateFiles'], "pruned{prune_r2}.proxies.maf{maf}.ld{proxy_r2}.dat"),
    run:
        dproxy = pandas.concat([pandas.read_csv(filename, sep='\t', usecols=['CHR1', 'POS1', 'POS2']) for filename in input.proxies], ignore_index=True)
        dproxy.rename(columns={'CHR1':'chrom', 'POS1':'indexPos', 'POS2':'proxyPos'}, inplace=True)
        dproxy.loc[:,'chrom'] = dproxy['chrom'].map(lambda x: "chr{x}".format(x=x))
        print(dproxy.head())
        print(dproxy.info())
        dmain = pandas.read_csv(input.main, sep='\t')
        dmain.rename(columns={'pos':'indexPos'}, inplace=True)
        dmain.loc[:,'chrom'] = dmain['chrom'].map(lambda x: "chr{x}".format(x=x))
    
        print(dmain.info())
        ndf = dmain.copy()
        ndf.loc[:,'proxyPos'] = ndf['indexPos']


        df = pandas.merge(dmain, dproxy, how="inner", on=['chrom','indexPos'])
        print(df.head())
        df = pandas.concat([ndf, df]).drop_duplicates()
        df.loc[:,'proxyStart'] = df['proxyPos'] - 1
        df.loc[:,'proxyStart'] = df['proxyStart'].astype(int)
        df.loc[:,'proxyPos'] = df['proxyPos'].astype(int)
        df.loc[:,'indexPos'] = df['indexPos'].astype(int)
        df.rename(columns={'SNP_x':'SNP','P_x':'P', 'effect':'slope'}, inplace=True)
        df[['chrom','proxyStart','proxyPos','indexPos', 'SNP', 'slope', 'P']].sort_values(by=['chrom','indexPos']).to_csv(output.main, sep='\t', index=False, na_rep="NA")
